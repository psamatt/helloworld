plugins {
    id "java"
    id "java-library"
    id "org.springframework.boot" version "2.5.0-M1"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.diffplug.spotless" version "5.10.1"
    id "pl.allegro.tech.build.axion-release" version "1.13.1"
}

group "com.psamatt"
project.version = scmVersion.version

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

sourceCompatibility = 11
targetCompatibility = 11

ext {
    lombokVersion = "1.18.18"
    junitVersion = "5.7.1"
    mockitoVersion = "3.5.13"
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springdoc:springdoc-openapi-ui:1.5.2"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.assertj:assertj-core:3.19.0"
}

test {
    useJUnitPlatform()
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        indentWithSpaces(4)
        googleJavaFormat("1.9").aosp()
    }
}

build.dependsOn 'spotlessApply'